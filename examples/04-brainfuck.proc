#!/usr/bin/env proc

int MAX_VAL = 255;
int CELL_CAPACITY = 30_000;
int cells[CELL_CAPACITY];
int cell_ptr = 0;

proc interpret(code) {
	int i, c, brackets;

	while code[i] {
		if code[i] == '>' { # ++p;
			if (cell_ptr == CELL_CAPACITY - 1) {
				return 255; # ERROR: out of bounds from the right
			}
			cell_ptr += 1;
		} else if code[i] == '<' { # --p;
			if (cell_ptr == 0) {
				return 254; # ERROR: out of bounds from the left
			}
			cell_ptr -= 1;
		} else if code[i] == '+' { # ++*p;
			if (cells[cell_ptr] == MAX_VAL) {
				cells[cell_ptr] = 0;
			} else {
				cells[cell_ptr] += 1;
			}
		} else if code[i] == '-' { # --*p;
			if (cells[cell_ptr] == 0) {
				cells[cell_ptr] = MAX_VAL;
			} else {
				cells[cell_ptr] -= 1;
			}
		} else if code[i] == '.' { # putchar(*p);
			PutChar(cells[cell_ptr]);
		} else if code[i] == ',' { # *p = getchar();
			c = GetChar();
			if c != -1 {
				cells[cell_ptr] = c;
			}
		} else if code[i] == '[' && !cells[cell_ptr] { # while (*p) {
			i += 1;
			while code[i] != ']' || brackets {
				if code[i] == '[' {
					brackets += 1;
				} else if code[i] == ']' {
					brackets -= 1;
				} else if !code[i] {
					return 253; # ERROR: unmatched [
				}
				i += 1;
			}
		} else if code[i] == ']' && cells[cell_ptr] { # } /* end while */
			i -= 1;
			while code[i] != '[' || brackets {
				if code[i] == '[' {
					brackets -= 1;
				} else if code[i] == ']' {
					brackets += 1;
				} else if i == 0 {
					return 252; # ERROR: unmatched ]
				}
				i -= 1;
			}
			i -= 1; # check loop condition again
		} else {
			# other characters are ignored
		}
		i += 1;
	}
	return 0;
}

proc main(argc, argv) {
	if (argc != 2) {
		return 1; # usage: ./brainfuck.proc CODE
	}
	return interpret(argv[1]);
}
